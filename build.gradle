buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'
apply plugin: 'curseforge'

ext.config = parseConfig(file('build.properties'))
ext.modVersion = "${config.version.mod.major}.${config.version.mod.minor}"

version = "${config.version.minecraft}-${modVersion}"
group= "com.dave.compactmachines" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "compactmachines"

minecraft {
    version = "${config.version.minecraft}-${config.version.forge}-${config.version.minecraft}"
    assetDir = "eclipse/assets"

    replaceIn "reference/Reference.java"
    replace "@MAJOR@", config.version.mod.major
    replace "@MINOR@", config.version.mod.minor
}

repositories {
    maven {
        name = "IGW"
        url = "http://maven.k-4u.nl/"
    }


    maven {
        name "FireBall API Depot"
        url "http://dl.tsr.me/artifactory/libs-release-local"
    }
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"

    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env
    compile "igwmod:IGW-Mod-1.7.10:1.1.1-17:userdev"

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

allprojects {
    if (System.getenv("TRAVIS") != null) {
        def tag = System.getenv("TRAVIS_TAG");
        if (tag != null && !tag.isEmpty()) {6A
            project.ext.releaseType = tag.contains("pre") ? "beta" : "release"
            version += tag.contains("pre") ? "-b${System.getenv("TRAVIS_BUILD_NUMBER")}" : ""
        } else {
            version += "-b${System.getenv("TRAVIS_BUILD_NUMBER")}"
            project.ext.releaseType = "beta"
        }
    }
}

processResources
{
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }

    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

curse {
    onlyIf {
        (!System.env.TRAVIS_TAG.isEmpty() || getCommitMsg.commitMsg().contains("[cf push]")) && System.env.CFKEY != null
    }

    apiKey = System.env.CFKEY
    projectId = "224218"
    changelog = """
            See https://github.com/thraaawn/CompactMachines
    """
    releaseType = project.hasProperty('releaseType') ? project.releaseType : ""
}

task getCommitMsg(type: Exec) {
    def commit = System.env.TRAVIS_COMMIT != null ? System.env.TRAVIS_COMMIT : ""
    commandLine "git log --format=%B -n 1 ${commit}".split(' ')
    standardOutput = new ByteArrayOutputStream()
    ext.commitMsg = {
        return standardOutput.toString()
    }
}

curse.dependsOn getCommitMsg

def parseConfig(File config) {
    config.withReader {
        def prop = new Properties()
        prop.load(it)
        return (new ConfigSlurper().parse(prop))
    }
}