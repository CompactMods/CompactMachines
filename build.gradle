buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        mavenCentral()
        mavenLocal()
    }

    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:5.1.+"
    }
}

plugins {
    id "idea"
    id "eclipse"
    id "maven-publish"
}

apply plugin: "net.minecraftforge.gradle"

def semver = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--match', 'v*'
            standardOutput = code
        }

        return Integer.parseInt(code.toString().trim().substring(1).split('-')[0])
    }
    catch (ignored) {
        return "4.0.0";
    }
}

def buildNumber = { ->
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--match', 'v*'
            standardOutput = code
        }

        return Integer.parseInt(code.toString().trim().split('-')[1])
    }
    catch (ignored) {
        return 0;
    }
}

def nightlyVersion = "${semver}-nightly.${buildNumber}"
def isRelease = (System.getenv("CM_RELEASE") ?: "false").equalsIgnoreCase("true")

task getBuildInfo() {
    println("Mod ID: ${mod_id}")
    println("Version: ${isRelease ? mod_version : nightlyVersion}")
    println("Semver Version: ${semver}")
    println("Nightly Build: ${nightlyVersion}")
}

version = isRelease ? mod_version : nightlyVersion
group = "dev.compactmods"
archivesBaseName = mod_id

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets {
    api {
        java {
            srcDir "src/api/java"
        }

        resources.srcDirs = []
        compileClasspath += configurations.getByName("minecraft")
    }

    datagen {
        java {
            srcDir "src/datagen/java"
        }

        compileClasspath += configurations.getByName("minecraft")
        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }

    main {
        java {
            srcDir "src/main/java"
        }

        resources {
            srcDir "src/main/resources"
            srcDir "src/generated/resources"
        }

        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }

    test {
        java { srcDir "src/test/java" }
        resources { srcDir "src/test/resources" }

        compileClasspath += sourceSets.api.output
        runtimeClasspath += sourceSets.api.output
    }
}

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: minecraft_version
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run/client')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--username', 'Dev##'
            args '--width', 1920
            args '--height', 1080

            mods {
                compactmachines {
                    source sourceSets.main
                    source sourceSets.api
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', mod_id

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            mods {
                compactmachines {
                    source sourceSets.main
                    source sourceSets.api
                    source sourceSets.test
                }
            }
        }

        data {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            property 'forge.logging.markers', '' // 'SCAN,REGISTRIES,REGISTRYDUMP'

            // Recommended logging level for the console
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            args '--mod', mod_id
            args '--existing', file('src/main/resources')
            args '--all'
            args '--output', file('src/generated/resources/')

            mods {
                compactmachines {
                    source sourceSets.datagen
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run/gametest')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"

            forceExit false
            
            mods {
                compactmachines {
                    source sourceSets.api
                    source sourceSets.main
                    source sourceSets.test
                }
            }
        }
    }
}

repositories {
    mavenLocal()

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    // location of the maven that hosts JEI files
    maven {
        name "Progwml Repo"
        url "https://dvs1.progwml6.com/files/maven"
    }

    // TheOneProbe
    maven {
        name 'tterrag maven'
        url "https://maven.tterrag.com/"
    }
}

dependencies {
    // Specify the version of Minecraft to use, If this is any group other then 'net.minecraft' it is assumed
    // that the dep is a ForgeGradle 'patcher' dependency. And it's patches will be applied.
    // The userdev artifact is a special name and will get all sorts of transformations applied to it.
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // JEI
    compileOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-${jei_mc_version}:${jei_version}")

    // The One Probe
    implementation(fg.deobf("curse.maven:theoneprobe-245211:3550084"))
//    compileOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}:api")
//    runtimeOnly fg.deobf("mcjty.theoneprobe:TheOneProbe-1.16:${top_version}")

    // Nicephore - Screenshots and Stuff
    runtimeOnly(fg.deobf("curse.maven:nicephore-401014:3574658"))

    // Shut up Experimental Settings - so we don't have to deal with that CONSTANTLY
    runtimeOnly(fg.deobf("curse.maven:shutupexperimental-407174:3544525"))

    // Testing Mods - Trash Cans, Pipez, Create, Refined Pipes, Pretty Pipes, Refined Storage
    runtimeOnly(fg.deobf("curse.maven:SuperMartijn642-454372:3544563"))
    runtimeOnly(fg.deobf("curse.maven:trashcans-394535:3544028"))
    runtimeOnly(fg.deobf("curse.maven:pipez-443900:3569514"))
    runtimeOnly(fg.deobf("curse.maven:flywheel-486392:3556768"))
    runtimeOnly(fg.deobf("curse.maven:create-328085:3556823"))
    runtimeOnly(fg.deobf("curse.maven:refinedpipes-370696:3570151"))
    runtimeOnly(fg.deobf("curse.maven:prettypipes-376737:3573145"))
    runtimeOnly(fg.deobf("curse.maven:refinedstorage-243076:3623324"))

    // Mekanism + Mek Generators - Tunnel testing
//    runtimeOnly(fg.deobf("curse.maven:mekanism-268560:3206392"))
//    runtimeOnly(fg.deobf("curse.maven:mekanismgenerators-268566:3206395"))
}

processResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

processTestResources {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}

jar {
    from sourceSets.main.output
    from sourceSets.api.output
    destinationDirectory = file("$rootDir/build-out")

    finalizedBy('reobfJar')

    manifest {
        attributes([
                "Specification-Title"     : "compactmachines",
                "Specification-Vendor"    : "",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : archiveVersion,
                "Implementation-Vendor"   : "",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task apiJar(type: Jar) {
    from sourceSets.api.output
    // Sources included because of MinecraftForge/ForgeGradle#369
    archiveClassifier = 'api'
    destinationDirectory = file("$rootDir/build-out")
}

artifacts {
    archives jar, apiJar
}

publishing {
    publications {
        release(MavenPublication) {
            artifactId = mod_id
            groupId = "dev.compactmods"

            artifacts {
                artifact jar
                artifact(apiJar) {
                    classifier = "api"
                }
            }
        }

        nightly(MavenPublication) {
            artifactId = mod_id
            groupId = "dev.compactmods.nightly"

            artifacts {
                artifact(jar)
                artifact(apiJar) {
                    classifier = "api"
                }
            }
        }
    }

    repositories {
        // GitHub Packages
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/CompactMods/CompactMachines"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}
